///$tab Criar report filter
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='$ ###0.00;-$ ###0.00';
SET TimeFormat='h:mm:ss TT';
SET DateFormat='M/D/YYYY';
SET TimestampFormat='M/D/YYYY h:mm:ss[.fff] TT';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:Î¼;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';

let vMinhaConexao = 'NOME_SPACO:NOME_CONEXAO';
let vMeuHost = 'meu-host.us.qlikcloud.com';

let vPath = 'lib://SPACE_NOME:NOME_LIB/PATH/TO/PLAN/';
let vPlan = 'criarFiltroQlikApi.xlsx';

Set ErrorMode = 0;

metaDadofiltro:
LOAD name, 
     description, 
     filterType,  
     fieldsByState,
     appId,
     criarFiltro
FROM
[$(vPath)$(vPlan)]
(ooxml, embedded labels, table is filterMeta);



FOR f=0 to NoOfRows('metaDadofiltro') -1

let vCriarFiltro = Peek('criarFiltro',$(f),'metaDadofiltro');

  if $(vCriarFiltro) = 1 then


      let vRequestBody = chr(39)&Chr(123);
      let vRequestBody = vRequestBody & '
          "name":"'&Peek('name',$(f),'metaDadofiltro')&'",
          "filterType":"'&Peek('filterType',$(f),'metaDadofiltro')&'",
          "filterV1_0": {
              "fieldsByState": {
                  "'&Peek('fieldsByState',$(f),'metaDadofiltro')&'":[
                  xxxxx
                  ]
              }
          },
          "description": "'&Peek('description',$(f),'metaDadofiltro')&'",
          "filterVersion": "filter-1.0"
  }'&Chr(39);

      fields:
      LOAD  
           field,  
           overrideValues, 
           selectExcluded,
           RowNo()-1 as Linha
      FROM
      [$(vPath)$(vPlan)]
      (ooxml, embedded labels, table is FilterField)
      Where filter=Peek('name',$(f),'metaDadofiltro')
      ;


      for Each v in FieldValueList('field')

              valueOffield:
              LOAD
                  Concat(valueOffield,',') as valueOffield;
              LOAD
                  '{
                      "valueType": "'&valueType&'",
                      "valueAsText": "'&value&'"'&if(valueType='number',',
                      "valueAsNumber":'&value)&'
                  }' as valueOffield
              FROM
              [$(vPath)$(vPlan)]
              (ooxml, embedded labels, table is FilterValue)
              Where filter=Peek('name',$(f),'metaDadofiltro') and  Match(valueType,'number','string') and field = '$(v)';
              ;				


      next

          listOfField:
          LOAD
              Concat(listOfField,',') as listOfField;
          LOAD
              '{
                  "name": "'&field&'",
                  "values": ['
                      &Peek('valueOffield',Linha,'valueOffield')&'
                          ],
                  "overrideValues": '&if(overrideValues=0,'false','true')&',
                  "selectExcluded": '&if(selectExcluded=0,'false','true')&'
              }' as listOfField
          Resident fields;
          drop Table valueOffield;
          drop Table fields;


          let vRequestBody = Replace($(vRequestBody),'xxxxx',Peek('listOfField',0,'listOfField'));
          let vRequestBody = replace(vRequestBody,'"', chr(34)&chr(34));        

          drop Table listOfField;

          /*
              =========================
              api criar filtro
              ========================
          */
          
          let vappId = Peek('appId',$(f),'metaDadofiltro');

          LIB CONNECT TO '$(vMinhaConexao)';
            CRETE_REPORT_FILTER:
            Load *, Date(Now()-1/24*3,'DD/MM/YYYY hh:mm:ss') AS [Data Hora Log] Where not IsNull(createdAt);
            SQL SELECT 
              "__KEY_root",
            (SELECT "id","name","ownerId","createdAt"
              FROM "Filter" PK "__KEY_data" FK "__FK_data")
            FROM JSON (wrap on) "root" PK "__KEY_root"
            WITH CONNECTION (
                              URL  "https://$(vMeuHost)/api/v1/apps/$(vappId)/report-filters",
                              BODY "$(vRequestBody)"
                              )
            ;
            Sleep 700;        
          DisConnect;

End If
NEXT
Set ErrorMode = 1;


